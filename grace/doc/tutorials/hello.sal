; A SAL program consists of comments, commands and symbolic
; expressions. Comments start with a semi-colon and appear in
; red. When SAL encounters a comment it ignores all the text until the
; end of that line.

; this is a comment

; A command is something that you execute to accomplish a task.
; Commands start in the very first column and appear in blue.  Here is
; a simple print command:

print "Hello, world!"

; To execute the command place your cursor at the end of the command
; line and press COMMAND-Enter. On the Mac the COMMAND key is the
; APPLE key just to the left of the space bar. On Windows and Linux the
; COMMAND key is the CONTROL key. The resets of executing the command
; will appear in the Console window

; Many expressions involve math. In math expressions the operators
; MUST be delimited by spaces. You can use parentheses to group and
; nest terms as you like. If you don't use parentheses then normal
; operator precedence applies:

print 1 + 2 * 3 

print (1 + 2) * 3 

; You will make many mistakes as you learn SAL. Most mistakes involve
; parentheses or undefined terms. When you execute an expression that
; contains an error you will see an error message printed in the
; Console window. Sometimes this message is useful, sometimes its not
; so useful. Such is life.

print (1 + 2 * 3

print foo

; Because SAL is a language the commands and expressions you type can
; become quite complex and will often span more than one line.  Here
; is an example of a moderately complex command. To execute it place
; your cursor just after the word 'end' and press COMMAND-Enter to see
; what it does.

begin
  with root = between(60, 90),
       third = root + pick(3,4) ,
       fifth = root + 7
   print "My random chord: ", note( list( root, third, fifth))
end

; The best way to learn how to express yourself in SAL is to study
; examples and try to figure out what they do BEFORE you execute
; them. You can use the COMMAND-D documentation lookup command to
; learn about the specific commands and functions you encounter as you
; work through these tutorials.

