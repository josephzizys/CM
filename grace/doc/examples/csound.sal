;; Sending data to Csound

;; This example assumes your Ports menu has a Csound port
;; available. You must also have Csound5 installed on your machine.
;; Select Ports>Csound>Open... to open Csound with the default
;; orchestra file ("grace.orc") selected in the dialog box.

;; the "cs:i" message sends data to instruments. its positional values
;; are interpreted: ins, time, dur, p4...

send "cs:i", 1, 0, 1, between(48, 72), 8000

;; the "cs:f" message sends func data to csound:

send "cs:f", 1, 0, 65536, 10, 1

send "cs:i", 2, 0, .5, between(60, 80), 5000

;; a process that sends csound data in realtime

define process ranins1(len, rhy, lb, ub, amp)
  run with dur = rhy * 2
    repeat len
    for k = lb then between(lb,ub)
    send "cs:i", 1, 0, dur, k, amp
    wait rhy
  end

sprout ranins1(10, .2, 60, 72, 1000)

;; If you send data to a closed Csound port the data gets collected in
;; a score. Select Ports>Csound>Close to close the open port.

;; Eval this loop to generate score data. Use Ports>Csound>Export to
;; print the score to the Console or to the clipboard. Use
;; Ports>Csound>Write... to write the data to a sound file.

loop for i from 0 below 10 by .5
  send "cs:i", 1, i, .25, between(60, 75), 8000
end

