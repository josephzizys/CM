;;; -*- syntax: Sal2; -*-

; steve reich's piano phase

process piano-phase (endtime, keys, rate)
  with pat = make-cycle(keys)
  while (elapsed() < endtime)
  send("mp:midi", key: next(pat), dur: rate)
  wait rate
end

; this plays the example in real time out your midi port

begin
  with keys = key({e4 fs4 b4 cs5 d5 fs4 e4 cs5 b4 fs4 d5 cs5}),
       stop = 20
  sprout(list(piano-phase(stop, keys, .167),
              piano-phase(stop, keys, .170)))
end

; this generates the example to a file called reich.mid in your home
; directory. (file generation happens without sound and much faster
; than real time)

begin
  with keys = key({e4 fs4 b4 cs5 d5 fs4 e4 cs5 b4 fs4 d5 cs5}),
       stop = 20
  sprout(list(piano-phase(stop, keys, .167),
              piano-phase(stop, keys, .170)) ,
         "reich.mid")
end