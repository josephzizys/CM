;;; -*- syntax: Sal2; -*-

;
;; Ghosts
;

; Ghosts gets its name from the fact that a short, randomly generated
; melody serves as the basis for computing other bits of dependent
; "ghost" structure that "echo" the melody in different musical ways
; but at much larger temporal levels in the piece. Ghosts generates
; the following gestures, each of which depends on certain tests
; applied to the main melody:

; 1. A High, temporally stretched version of the melody

process hitone (knum, dur)
  repeat 1 
  send("mp:midi", key: knum + 24, dur: dur, amp: .5)
end

sprout(hitone(between(60, 67), 1))

; 2. A low, percussive "thump" that accompanies low melodic tones

process thump (knum)
  repeat 1
  send("mp:midi", key: knum - 18, dur: .05,  amp: .4)
  send("mp:midi", key: knum - 23, dur: .05,  amp: .4)
end

sprout(thump(between(60, 67)))

; 3. Distant strums that accompany the high stretched melody but at
; even larger time scales.

process strum (knum, rate)
  repeat 5
  for k from ( 39 + modulo( knum, 13) ) by 13
  send("mp:midi", key: k, amp: .3, dur: 10)
  wait rate
end

sprout(strum(between(60, 67), .2))

; The ghosts process.
	
process ghosts ()
  repeat 12
  for here = elapsed()
  for ahead = (here + 1/2) * 2
  for main = between( 53, 77)
  for high? = (main >=  65)
  for amp = #? (high?, .6, .4)
  for rhy = pick(1/4, 1/2, 3/4)
  send("mp:midi",  key: main, dur: rhy + .2, amp: amp)
  if (high?)
    sprout(hitone(main, ahead), ahead)
    sprout(strum(main, rhy / 4), ahead * 2)
  end
  if (rhy = 3/4)
    sprout(thump( main), 1/2)
  end
  wait rhy
end

sprout(ghosts())

