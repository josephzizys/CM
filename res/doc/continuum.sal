;;; -*- syntax: Sal2;  -*-

;; a la maniere de 'continuum' (ligeti)

process continuum (tmp, low-keys, high-keys, rhy-pat)
  for low in low-keys
  for high in high-keys
  for dur = next (rhy-pat)
  sprout(register(tmp, dur, low, high, .4))
  wait dur
end

process register (rhy, dur, low, high, amp)
  with key-pat = make-heap( scale(high - low + 1, low, 1))
  for key = next(key-pat)
  until elapsed() >= dur
  send( "mp:midi", key: key, dur: rhy, amp: amp)
  wait rhy
end

;; set channel 0 to harpsichord 

send("mp:instruments", 6)

sprout(continuum(.075,
                 {60 59 58 57 56 55 54 53 52
                  53 54 55 56 57 58 59 60 61 62 63 64 65 66 67 68
                  69 70 71 72 73 74 75 76 77 78
                  79 80 82 83 84 85 86 87 88 89},
                 {62 63 64 65 66 67 68 69 70 
                  70 70 70 70 70 70 70 70 70 70 70 70 70 70 70 70
                  71 72 73 74 76 79 83 86 88 89
                  89 89 89 89 89 89 89 89 89 89},
                 make-weighting({.5 1 1.5 2 2.5})
                 ))
